{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";
forall X -> int is_null? (X x) asm "ISNULL";


() recv_internal() {
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  tuple stack = null();
  stack~push_back(tree);
  while (~ stack.is_null?()) {
      cell s = stack~pop_back();

      if (cell_hash(s) == hash) {
        return s;
      }
      slice ss = s.begin_parse();

      repeat (ss.slice_refs()) {
          stack~push_back(ss~load_ref());
      }
  }
  return begin_cell().end_cell();
}
